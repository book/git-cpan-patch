#!/usr/bin/env perl

use 5.10.0;

use strict;
use warnings;

use autodie;

use Git;

use Parse::BACKPAN::Packages;

my $module = shift || die("Usage: git cpan-init Foo-Bar\n");

say "opening backpan index";
my $backpan = Parse::BACKPAN::Packages->new;

Git::command_noisy("init");

my $repo = Git->repository;

my @dists = $backpan->distributions($module);

foreach my $dist ( @dists ) {
     say "importing " . $dist->distvname;
     my $url  = sprintf 'http://backpan.perl.org/%s', $dist->prefix;
     $repo->command_noisy("cpan-import", $url);
}

$repo->command_noisy("checkout", "-t", "-b", "master", "cpan/master");

__END__

=pod

=head1 NAME

git-backpan-init - Initialize a repository for a CPAN module with full history
from the backpan.

=head1 SYNOPSIS

    # this command always takes a dist name

    % mkdir Foo-Bar
    % cd Foo-Bar
    % git backpan-init Foo-Bar

=head1 DESCRIPTION

This command is like C<cpan-init> except it imports all the historical versions
of a module as well, allowing you to run C<git bisect> or to browse history.

=cut
